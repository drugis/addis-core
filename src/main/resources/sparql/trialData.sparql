PREFIX ontology: <http://trials.drugis.org/ontology#>
PREFIX dataset: <http://trials.drugis.org/datasets/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX entity: <http://trials.drugis.org/entities/>
PREFIX concept: <http://trials.drugis.org/concepts/>
PREFIX instance: <http://trials.drugis.org/instances/>
PREFIX graph: <http://trials.drugis.org/graphs/>
PREFIX qudt: <http://qudt.org/schema/qudt#>

SELECT DISTINCT
?graph
?studyName
?drug
?isPrimaryEpoch
?interventionLabel
?defaultMeasurementMoment
?drugInstance
?treatmentNode
?outcomeTypeUri
?outcomeInstance
?outcomeTypeUid
?outcomeInstanceLabel
?arm
?armLabel
?mean
?stdDev
?stdErr
?count
?sampleSize
?exposure
?survivalTimeScale
?fixedDoseValue
?fixedDoseDosingPeriodicity
?fixedDoseUnitConcept
?fixedDoseUnitLabel
?fixedDoseUnitMultiplier
?maxDoseValue
?maxDoseDosingPeriodicity
?maxDoseUnitConcept
?maxDoseUnitLabel
?maxDoseUnitMultiplier
?measurementType
?measurementMoment
?measurementMomentLabel
?minDoseValue
?minDoseDosingPeriodicity
?minDoseUnitConcept
?minDoseUnitLabel
?minDoseUnitMultiplier
(COALESCE(?treatmentTypeOptional, "http://trials.drugis.org/ontology#SimpleDrugTreatment") AS ?treatmentType)
WHERE {
  BIND ($graphSelector AS ?graph) .
  GRAPH $graphSelector {
    ?study rdfs:label ?studyName .
    ?study ontology:has_outcome ?outcomeInstance .
    $outcomeUnionString .
    ?outcomeInstance
      rdfs:label ?outcomeInstanceLabel ;
      a ?outcomeTypeUid ;
      rdfs:label ?outcomeInstanceLabel ;
      ontology:of_variable [
        owl:sameAs ?outcomeTypeUri ;
        ontology:measurementType ?measurementType
      ] .
    $interventionUnionString .
  }
  GRAPH $graphSelector {
    ?drugInstance owl:sameAs ?drug .
    ?drugInstance rdfs:label ?interventionLabel .
    ?study ontology:has_arm ?arm .
    ?study ontology:has_primary_epoch ?primaryEpoch .
    ?activity a ontology:TreatmentActivity ;
      ontology:has_activity_application [
        ontology:applied_to_arm ?arm ;
        ontology:applied_in_epoch ?epoch
      ] ;
      ontology:has_drug_treatment ?treatmentNode .
    ?treatmentNode ontology:treatment_has_drug ?drugInstance .

    ?epoch rdfs:label ?epochLabel .
    ?arm rdfs:label ?armLabel .

    BIND (?epoch = ?primaryEpoch AS ?isPrimaryEpoch) .

    ?defaultMeasurementMoment
      ontology:relative_to_epoch ?primaryEpoch ;
      ontology:relative_to_anchor ontology:anchorEpochEnd ;
      ontology:time_offset "PT0S"^^xsd:duration .

    ?measurementMoment rdfs:label ?measurementMomentLabel .

    ?measurement
      ontology:of_moment ?measurementMoment ;
      ontology:of_outcome ?outcomeInstance ;
      ontology:of_group ?arm .

    OPTIONAL {
      ?measurement ontology:mean ?mean .
    }
    OPTIONAL {
      ?measurement ontology:standard_deviation ?stdDev .
    }
    OPTIONAL {
      ?measurement ontology:standard_error ?stdErr .
    }
    OPTIONAL {
      ?measurement ontology:count ?count .
    }
    OPTIONAL {
      ?measurement ontology:sample_size ?sampleSize .
    }
    OPTIONAL {
      ?measurement ontology:exposure ?exposure .
      ?outcomeInstance ontology:survival_time_scale ?survivalTimeScale .
    }

    OPTIONAL {
        ?treatmentNode
          a ontology:FixedDoseDrugTreatment ;
          a ?treatmentTypeOptional ;
          ontology:treatment_dose [
            rdf:value ?fixedDoseValue ;
            ontology:dosingPeriodicity ?fixedDoseDosingPeriodicity ;
            ontology:unit ?fixedDoseUnit
          ] .
          { ?fixedDoseUnit
                rdfs:label ?fixedDoseUnitLabel ;
                a ?fixedDoseUnitConcept .
            OPTIONAL { ?fixedDoseUnit qudt:conversionMultiplier ?fixedDoseUnitMultiplier }
          } UNION {
            ?fixedDoseUnit
                rdfs:label ?fixedDoseUnitLabel ;
                a ontology:Unit ;
                owl:sameAs ?fixedDoseUnitConcept .
            OPTIONAL { ?fixedDoseUnit qudt:conversionMultiplier ?fixedDoseUnitMultiplier }
          } .
          FILTER (?fixedDoseUnitConcept != ontology:Unit) .
    } .
    OPTIONAL {
        ?treatmentNode
          a ontology:TitratedDoseDrugTreatment ;
          a ?treatmentTypeOptional ;
          ontology:treatment_max_dose [
            rdf:value ?maxDoseValue ;
            ontology:dosingPeriodicity ?maxDoseDosingPeriodicity ;
            ontology:unit ?maxDoseUnit
          ] ;
          ontology:treatment_min_dose [
                      rdf:value ?minDoseValue ;
                      ontology:dosingPeriodicity ?minDoseDosingPeriodicity ;
                      ontology:unit ?minDoseUnit
           ] .
          { ?maxDoseUnit
              rdfs:label ?maxDoseUnitLabel ;
              a ?maxDoseUnitConcept .
            OPTIONAL { ?maxDoseUnit qudt:conversionMultiplier ?maxDoseUnitMultiplier }
          } UNION {
            ?maxDoseUnit
                rdfs:label ?maxDoseUnitLabel ;
                a ontology:Unit ;
                owl:sameAs ?maxDoseUnitConcept .
            OPTIONAL { ?maxDoseUnit qudt:conversionMultiplier ?maxDoseUnitMultiplier }
          } .
          FILTER (?maxDoseUnitConcept != ontology:Unit) .
          { ?minDoseUnit
                rdfs:label ?minDoseUnitLabel ;
                a ?minDoseUnitConcept .
            OPTIONAL { ?minDoseUnit qudt:conversionMultiplier ?minDoseUnitMultiplier }
          } UNION {
              ?minDoseUnit
                  rdfs:label ?minDoseUnitLabel ;
                  a ontology:Unit ;
                  owl:sameAs ?minDoseUnitConcept .
            OPTIONAL { ?minDoseUnit qudt:conversionMultiplier ?minDoseUnitMultiplier }
          } .
          FILTER (?minDoseUnitConcept != ontology:Unit) .

    } .
    {
      SELECT ?study ?arm
      WHERE {
       ?activity a ontology:TreatmentActivity ;
             ontology:has_activity_application [
               ontology:applied_to_arm ?arm ;
               ontology:applied_in_epoch ?epoch
             ] ;
             ontology:has_drug_treatment [
               ontology:treatment_has_drug ?drugInstance
             ] .
      }
    }
  }
}